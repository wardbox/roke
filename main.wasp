app roke {
  wasp: {
    version: "^0.15.1"
  },
  title: "Roke",
  head: [
    "<script defer data-domain='example.com' src='https://plausible.io/js/script.js'></script>",
  ],
  auth: {
    userEntity: User,
    methods: {
      email: {
        fromField: {
          name: "roke.dev",
          email: "wizard@roke.dev"
        },
        emailVerification: {
          clientRoute: EmailVerificationRoute,
          getEmailContentFn: import { getVerificationEmailContent } from "@src/auth/email",
        },
        passwordReset: {
          clientRoute: PasswordResetRoute,
          getEmailContentFn: import { getPasswordResetEmailContent } from "@src/auth/email",
        },
        userSignupFields: import { getEmailUserFields } from "@src/auth/user-signup-fields",
      }
    },
    onAuthSucceededRedirectTo: "/note-example",
    onAuthFailedRedirectTo: "/login"
  },
  client: {
    rootComponent: import Root from "@src/Root",
    setupFn: import setupClient from "@src/setup"
  },
  emailSender: {
    provider: Mailgun,
  }
}

//#region Routes
route Landing { path: "/", to: Landing }
route Guide { path: "/guide", to: Guide }
route NoteExample { path: "/note-example", to: NoteExample }
route Motion { path: "/motion", to: Motion }
route Utils { path: "/utils", to: Utils }
route Profile { path: "/profile/:id", to: ProfilePage }
route LoginRoute { path: "/login", to: LoginPage }
route SignupRoute { path: "/signup", to: SignupPage }
route RequestPasswordResetRoute { path: "/request-password-reset", to: RequestPasswordResetPage }
route PasswordResetRoute { path: "/password-reset", to: PasswordResetPage }
route EmailVerificationRoute { path: "/email-verification", to: EmailVerificationPage }
//#endregion

//#region Pages
page Landing {
  component: import Landing from "@src/client/landing",
}

page Guide {
  component: import Guide from "@src/client/guide",
}

page NoteExample {
  component: import NoteExample from "@src/client/example/note-example",
}

page Motion {
  component: import Motion from "@src/client/motion",
}

page Utils {
  component: import Utils from "@src/client/utils",
}

page ProfilePage {
  component: import Profile from "@src/client/profile",
  authRequired: true,
}

page LoginPage {
  component: import { Login } from "@src/auth/auth",
}

page SignupPage {
  component: import { Signup } from "@src/auth/auth",
}

page RequestPasswordResetPage {
  component: import { RequestPasswordReset } from "@src/auth/auth",
}

page PasswordResetPage {
  component: import { PasswordReset } from "@src/auth/auth",
}

page EmailVerificationPage {
  component: import { EmailVerification } from "@src/auth/auth",
}
//#endregion

//#region Notes
action createNote {
  fn: import { createNote } from "@src/client/example/operations",
  entities: [User, Note],
}

query getNotes {
  fn: import { getNotes } from "@src/client/example/operations",
  entities: [User, Note],
}

action updateNote {
  fn: import { updateNote } from "@src/client/example/operations",
  entities: [User, Note],
}

action deleteNote {
  fn: import { deleteNote } from "@src/client/example/operations",
  entities: [User, Note],
}
//#endregion

//#region Jobs
job exampleJob {
  executor: PgBoss,
  perform: {
    fn: import { exampleJob } from "@src/jobs/example-job",
  },
  entities: [User, Note],
  schedule: {
    cron: "0 0 * * 0"
  }
}
//#endregion
